if (project.projectDir.parentFile.parentFile != rootProject.projectDir) return

// https://developer.android.com/studio/build/maven-publish-plugin?hl=zh-cn
apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    Properties properties = new Properties()
    InputStream inputStream = rootProject.file('local.properties').newDataInputStream()
    properties.load(inputStream)

    _projectGroupId = 'cn.6xyun.android'
    _projectArtifactId = getLibraryArtifactId()
    _projectVersion = getLibraryVersionName()
    // GPG公钥ID后8位
    _signingKeyId = properties.getProperty('signing.keyId')
    _signingPassword = properties.getProperty('signing.password')
    _signingSecretKeyRingFile = properties.getProperty('signing.secretKeyRingFile')
    _sonatypeUsername = properties.getProperty('ossrhUsername')
    _sonatypePassword = properties.getProperty('ossrhPassword')

    inputStream.close()
}
ext."signing.keyId" = _signingKeyId
ext."signing.password" = _signingPassword
ext."signing.secretKeyRingFile" = _signingSecretKeyRingFile

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.source
    classifier = 'sources'
}

afterEvaluate {
    publishing {
        publications {
            sonatype(MavenPublication) {
                from components.release
                groupId _projectGroupId
                artifactId _projectArtifactId
                version _projectVersion
                artifact androidSourcesJar
                pom {
                    name = _projectArtifactId
                    description = 'A set of Android libraries'
                    url = 'https://github.com/lx0758/Android'
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'ttps://github.com/lx0758/Android/blob/master/LICENSE.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'lx0758'
                            name = 'Liux'
                            email = 'lx0758@qq.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/lx0758/Android'
                        connection = 'scm:git@github.com:lx0758/Android.git'
                        developerConnection = 'scm:git@github.com:lx0758/Android.git'
                    }
                }
            }
        }
        repositories {
            maven {
                name 'sonatype'
                def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username _sonatypeUsername
                    password _sonatypePassword
                }
            }
        }
    }
    signing {
        sign publishing.publications
    }
}