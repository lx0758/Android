if (!project.projectDir.path.startsWith(rootProject.projectDir.path)) return

def releaseGroupId = 'com.liux.android'
def releaseArtifactId = project.getLibraryArtifactId()
def releaseVersion = project.getLibraryVersionName()
def releaseUrl = 'https://github.com/lx0758/Android'
def releaseLicense = 'MIT'

Properties properties = new Properties()
InputStream inputStream = rootProject.file('local.properties').newDataInputStream()
properties.load(inputStream)
def mavenUser = properties.getProperty('mavenUser')
def mavenPassword = properties.getProperty('mavenPassword')
def bintrayUser = properties.getProperty('bintrayUser')
def bintraykey = properties.getProperty('bintraykey')
inputStream.close()

apply plugin: 'maven-publish'
task androidSourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}
afterEvaluate {
    publishing {
        publications {
            JCenter(MavenPublication) {
                artifact androidSourcesJar
                // Applies the component for the release build variant.
                from components.release
                // You can then customize attributes of the publication as shown below.
                groupId releaseGroupId
                artifactId releaseArtifactId
                version releaseVersion
            }
        }
        repositories {
            maven {
                url = "http://domain/repository/maven-releases/"
                credentials {
                    username = mavenUser
                    password = mavenPassword
                }
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'
static def getSafePackageName(String artifactId) {
    switch (artifactId) {
        case "view": return "lib-view"
    }
    return artifactId
}
bintray {
    user = bintrayUser
    key = bintraykey
    publications = ['JCenter']
    pkg {
        userOrg = ''
        repo = 'android'
        name = getSafePackageName(releaseArtifactId)
        version {
            name = releaseVersion
        }

        websiteUrl = releaseUrl
        vcsUrl = releaseUrl
        licenses = [
                releaseLicense
        ]
    }
    dryRun = false
    override = false
    publish = true
}