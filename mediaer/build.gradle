apply plugin: 'com.android.library'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION as String
    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode getLibraryVersionCode()
        versionName getLibraryVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'proguard-rules-aar.txt'
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api ('com.github.yalantis:ucrop:2.2.4', {
        exclude group: 'com.squareup.okhttp3'
    })
    api 'com.github.bumptech.glide:glide:4.10.0'
    api ('com.github.bumptech.glide:okhttp3-integration:4.10.0', {
        exclude group: 'com.squareup.okhttp3'
    })
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'
    compileOnly 'com.liux.android:http:0.2.+'
}

apply from: '../release.gradle'
static def getLibraryArtifactId() {
    return 'picturer'
}
static def getLibraryVersionName() {
    return '0.2.0'
}
static def getLibraryVersionCode() {
    def version = getLibraryVersionName().replace("-SNAPSHOT", "")
    def versions = version.split("\\.")
    return Integer.parseInt(versions[0]) * 10000 + Integer.parseInt(versions[1]) * 100 + Integer.parseInt(versions[2]) * 1
}